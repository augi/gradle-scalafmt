plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'org.jreleaser' version '1.22.0'
    id 'com.gradle.plugin-publish' version '1.3.1'
}

group 'cz.augi'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

repositories {
    mavenCentral()
}

dependencies {
    api gradleApi()
    api 'org.scalameta:scalafmt-dynamic_2.13:2.7.5'
    api 'org.scala-lang.modules:scala-xml_2.13:2.4.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation gradleTestKit()
    testImplementation ('org.spockframework:spock-core:2.4-groovy-5.0') {
        exclude group:'org.codehaus.groovy'
    }
    testImplementation 'org.junit.platform:junit-platform-launcher:1.14.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

project.ext.set('gradle.publish.key', System.getenv('GRADLE_PORTAL_KEY'))
project.ext.set('gradle.publish.secret', System.getenv('GRADLE_PORTAL_SECRET'))

gradlePlugin {
    website = 'https://github.com/augi/gradle-scalafmt'
    vcsUrl = 'https://github.com/augi/gradle-scalafmt'
    plugins {
        wartremoverPlugin {
            id = 'cz.augi.gradle.scalafmt'
            displayName = 'Gradle scalafmt plugin'
            description = 'Gradle plugin to apply scalafmt (Scala linting tool)'
            implementationClass = 'cz.augi.gradle.scalafmt.ScalafmtPlugin'
            tags.set(['scala', 'scalafmt'])
        }
    }
}


java {
    withJavadocJar()
    withSourcesJar()
}

afterEvaluate {
    publishing {
        publications.forEach {
            it.pom {
                name = 'Gradle scalafmt Plugin'
                description = 'Gradle plugin to apply scalafmt (Scala linting tool)'
                url = 'https://github.com/augi/gradle-scalafmt'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'augi'
                        name = 'Michal August√Ωn'
                        email = 'augustyn.michal@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/augi/gradle-scalafmt.git'
                    developerConnection = 'scm:git:ssh://github.com/augi/gradle-scalafmt.git'
                    url = 'https://github.com/augi/gradle-scalafmt'
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        String base64Key = System.getenv('SIGNING_KEY')
        if (base64Key) {
            secretKey = new String(Base64.decoder.decode(base64Key))
        }
        String base64PublicKey = System.getenv('SIGNING_PUBLIC_KEY')
        if (base64PublicKey) {
            publicKey = new String(Base64.decoder.decode(base64PublicKey))
        }
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
    release {
        github {
            enabled = false
        }
    }
}
